configfile: "config.yaml"

rule all:
	input:
		expand('fastq/{sample}.fastq.gz', sample=config['samples']),
		expand('fastqc/raw/{sample}_fastqc.html', sample=config['samples']),
		expand('trim/{sample}.trim3adapter.fastq.gz', sample=config['samples']),
		expand('fastqc/trim/{sample}.trim3adapter_fastqc.html', sample=config['samples']),
		expand('dedup/{sample}.dedup.fa', sample=config['samples']),
		expand('mapping/{sample}', sample=config['samples']),
		expand('mapping/{sample}/Aligned.sortedByCoord.out.bam', sample=config['samples']),
		expand('table/{sample}_aligned_stat', sample=config['samples']),
		expand('xlsites/{sample}_reads_unique.bed', sample=config['samples']),
		expand('xlsites/{sample}_reads_multiple.bed', sample=config['samples']),
		expand('xlsites/{sample}_reads_skipped.bam', sample=config['samples']),
		expand('peaks/{sample}_reads_unique_peaks.bed', sample=config['samples']),
		expand('peaks/{sample}_reads_unique_peaks_scores.tsv', sample=config['samples']),
		expand('peaks/{sample}_reads_unique_peaks_clusters.bed', sample=config['samples']),
		expand('xlsites/{sample}_reads_unique_anno_geneid.tab', sample=config['samples']),
		expand('xlsites/{sample}_reads_unique_anno_biotype.tab', sample=config['samples']),
		expand('peaks/{sample}_reads_unique_peaks_anno_biotype.tab', sample=config['samples']),
		expand('peaks/{sample}_reads_unique_peaks_anno_geneid.tab', sample=config['samples']),
		expand('peaks/{sample}_reads_unique_peaks_summary.tab', sample=config['samples']),
		expand('xlsites/{sample}_reads_unique_summary.tab', sample=config['samples']),
		expand('figure/{sample}_crosslink_sites_distr.pdf', sample=config['samples']),
		expand('peaks/{sample}_reads_unique_peaks.bedGraph', sample=config['samples']),
		expand('track/{sample}_reads_unique_peaks_CPM.tdf', sample=config['samples']),
		expand('xlsites/{sample}_reads_unique.bedGraph', sample=config['samples']),
		expand('track/{sample}_reads_unique_all_CPM.tdf', sample=config['samples']),
		'table/stats_table.tsv',


rule split_fastq:
	input:
		config['raw_reads'],
	output:
		'fastq/{sample}.fastq.gz'
	params:
		barcode = config['barcode']
	shell:
		'bin/split_fastq_by_barcode_{params.barcode}.py {input} {output}'

rule fastqc_raw:
	input:
		'fastq/{sample}.fastq.gz'
	output:
		'fastqc/raw/{sample}_fastqc.html'
	params:
		conda = config['conda_path']
	shell:
		'{params.conda}/fastqc -o fastqc/raw {input}'

rule trim3adapter:
	input:
		'fastq/{sample}.fastq.gz'
	output:
		'trim/{sample}.trim3adapter.fastq.gz'
	params:
		conda = config['conda_path'],
		adapt = config['adapt'],
		error = config['adapt_error']
	shell:
		 "{params.conda}/cutadapt --error-rate {params.error} -f fastq -a {params.adapt} -m25 {input} | gzip -c > {output}"

rule fastqc_trim:
	input:
		'trim/{sample}.trim3adapter.fastq.gz'
	output:
		'fastqc/trim/{sample}.trim3adapter_fastqc.html'
	params:
		conda = config['conda_path']
	shell:
		'{params.conda}/fastqc -o fastqc/trim {input}'

rule remove_PCR_duplicates:
	input:
		'trim/{sample}.trim3adapter.fastq.gz'
	output:
		'dedup/{sample}.dedup.fa'
	params:
		conda = config['conda_path'],
		barcode = config['barcode']
	shell:
		 "zcat {input} | {params.conda}/fastq_to_fasta -Q33 -r -n | bin/split_barcode_{params.barcode}.py | sort | uniq | cut -f1 | bin/seq2fasta.py > {output}"

rule mapping:
	input:
		'dedup/{sample}.dedup.fa'
	output:
		dir = 'mapping/{sample}',
		bam = 'mapping/{sample}/Aligned.sortedByCoord.out.bam'
	params:
		conda = config['conda_path'],
		genome = config['genome'],
		annotation = config['annotation'],
		mismatch = config['mismatch'],
		cpu = config['cpu']
	shell:
		'mkdir -p {output.dir} && export ICOUNT_TMP_ROOT={output.dir}.tmp && {params.conda}/iCount mapstar -mis {params.mismatch} --threads {params.cpu} {input} {params.genome} {output.dir} --annotation {params.annotation}'

rule bam_stat:
	input:
		'mapping/{sample}/Aligned.sortedByCoord.out.bam'
	output:
		'table/{sample}_aligned_stat'
	shell:
		'samtools view {input} | bin/sam_stat.py > {output}'

rule xlsites_reads:
	input:
		'mapping/{sample}/Aligned.sortedByCoord.out.bam'
	output:
		'xlsites/{sample}_reads_unique.bed',
		'xlsites/{sample}_reads_multiple.bed',
		'xlsites/{sample}_reads_skipped.bam'
	params:
		conda = config['conda_path']
	shell:
		'{params.conda}/iCount xlsites {input} {output} --group_by start --quant reads'

rule significant_xlsites:
	input:
		'xlsites/{sample}_reads_unique.bed',
	output:
		bed = 'peaks/{sample}_reads_unique_peaks.bed',
		tsv = 'peaks/{sample}_reads_unique_peaks_scores.tsv'
	params:
		conda = config['conda_path'],
		segmentation = config['segmentation'],
		FDR = config['FDR']
	shell:
		'{params.conda}/iCount peaks {params.segmentation} {input} {output.bed} --scores {output.tsv} --fdr {params.FDR}'

rule clusters:
	input:
		'peaks/{sample}_reads_unique_peaks.bed'
	output:
		'peaks/{sample}_reads_unique_peaks_clusters.bed'
	params:
		conda = config['conda_path'],
		cluster_dist = config['cluster_dist']
	shell:
		'{params.conda}/bedtools cluster -d {params.cluster_dist} -i {input} > {output}'
 
rule xlsites_sig_annotate_biotype:
	input:
		'peaks/{sample}_reads_unique_peaks.bed'
	output:
		'peaks/{sample}_reads_unique_peaks_anno_biotype.tab'
	params:
		conda = config['conda_path'],
		segmentation = config['segmentation']
	shell:
		'{params.conda}/iCount annotate --subtype biotype {params.segmentation} {input} {output}'

rule xlsites_sig_annotate_gene_id:
	input:
		'peaks/{sample}_reads_unique_peaks.bed'
	output:
		'peaks/{sample}_reads_unique_peaks_anno_geneid.tab'
	params:
		conda = config['conda_path'],
		segmentation = config['segmentation']
	shell:
		'{params.conda}/iCount annotate --subtype gene_id {params.segmentation} {input} {output}'

rule xlsites_ALL_annotate_biotype:
	input:
		'xlsites/{sample}_reads_unique.bed'
	output:
		'xlsites/{sample}_reads_unique_anno_biotype.tab'
	params:
		conda = config['conda_path'],
		segmentation = config['segmentation']
	shell:
		'{params.conda}/iCount annotate --subtype biotype {params.segmentation} {input} {output}'

rule xlsites_ALL_annotate_gene_id:
	input:
		'xlsites/{sample}_reads_unique.bed'
	output:
		'xlsites/{sample}_reads_unique_anno_geneid.tab'
	params:
		conda = config['conda_path'],
		segmentation = config['segmentation']
	shell:
		'{params.conda}/iCount annotate --subtype gene_id {params.segmentation} {input} {output}'

rule xlsites_summary_all:
	input:
		'xlsites/{sample}_reads_unique_anno_geneid.tab'
	output:
		'xlsites/{sample}_reads_unique_summary.tab'
	params:
		conda = config['conda_path'],
	shell:
		'bin/sites_summary.sh {input} {output}'

rule xlsites_summary_sig:
	input:
		'peaks/{sample}_reads_unique_peaks_anno_geneid.tab'
	output:
		'peaks/{sample}_reads_unique_peaks_summary.tab'
	params:
		conda = config['conda_path'],
	shell:
		'bin/sites_summary.sh {input} {output}'

rule xlsites_distr:
	input:
		'peaks/{sample}_reads_unique_peaks_summary.tab',
		'xlsites/{sample}_reads_unique_summary.tab'
	output:
		'figure/{sample}_crosslink_sites_distr.pdf'
	shell:
		'bin/xlsites_distr.R {input} {output}'

rule stats_tab:
	input:
		["xlsites/{sample}_reads_unique.bed".format(sample=x) for x in config['samples']],
		["peaks/{sample}_reads_unique_peaks.bed".format(sample=x) for x in config['samples']],
		["peaks/{sample}_reads_unique_peaks_clusters.bed".format(sample=x) for x in config['samples']],
	output:
		'table/stats_table.tsv'
	shell:
		'bin/stats.sh no_rep'

rule bed2bedgraph_sig:
	input:
		'peaks/{sample}_reads_unique_peaks.bed'
	output:
		'peaks/{sample}_reads_unique_peaks.bedGraph'
	shell:
		"bin/normalized_bedgraph.sh {input}"

rule bed2bedgraph_all:
	input:
		'xlsites/{sample}_reads_unique.bed'
	output:
		'xlsites/{sample}_reads_unique.bedGraph'
	shell:
		"bin/normalized_bedgraph.sh {input}"

rule bedgraph2tdf_sig:
	input:
		'peaks/{sample}_reads_unique_peaks.bedGraph'
	output:
		'track/{sample}_reads_unique_peaks_CPM.tdf'
	params:
		IGV = config['IGV']
	shell:
		"igvtools toTDF {input} {output} {params.IGV}"

rule bedgraph2tdf_all:
	input:
		'xlsites/{sample}_reads_unique.bedGraph'
	output:
		'track/{sample}_reads_unique_all_CPM.tdf'
	params:
		IGV = config['IGV']
	shell:
		"igvtools toTDF {input} {output} {params.IGV}"
